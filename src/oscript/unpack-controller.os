
Перем Распаковщик;
Перем Параметры;

Процедура Синхронизировать()

	СоздатьРаспаковщик();
	
	Попытка
		
		Если Не Распаковщик.СинхронизироватьХранилищеКонфигурацийСГит(Параметры) Тогда
			ВызватьИсключение "Синхронизация завершилась неудачно. См. лог";
		КонецЕсли;
	Исключение
		Распаковщик.УдалитьЗарегистрированныеВременныеФайлы();
		ВызватьИсключение;
	КонецПопытки;
	
	Распаковщик.УдалитьЗарегистрированныеВременныеФайлы();
	
КонецПроцедуры

Процедура ПолучитьСУдаленногоУзла()
	
	СоздатьРаспаковщик();
	
	Попытка
		
		Результат = Распаковщик.ВыполнитьGitPull(Параметры.КаталогВыгрузки, Параметры.GitURL, "master");
		Если Результат <> 0 Тогда
			ВызватьИсключение "Git pull завершился с кодом <"+Результат+">";
		КонецЕсли;
		
	Исключение
		Распаковщик.УдалитьЗарегистрированныеВременныеФайлы();
		ВызватьИсключение;
	КонецПопытки;
	
	Распаковщик.УдалитьЗарегистрированныеВременныеФайлы();
	
КонецПроцедуры

Процедура ОтправитьНаУдаленныйУзел()
	СоздатьРаспаковщик();
	
	Попытка
		
		Результат = Распаковщик.ВыполнитьGitPush(Параметры.КаталогВыгрузки, Параметры.GitURL, "master");
		Если Результат <> 0 Тогда
			ВызватьИсключение "Git push завершился с кодом <"+Результат+">";
		КонецЕсли;
		
	Исключение
		Распаковщик.УдалитьЗарегистрированныеВременныеФайлы();
		ВызватьИсключение;
	КонецПопытки;
	
	Распаковщик.УдалитьЗарегистрированныеВременныеФайлы();
	
КонецПроцедуры

Процедура ПрочитатьПараметры()
	Параметры = ПолучитьПараметрыИзОкружения();
КонецПроцедуры

Функция АбсолютныйПуть(Знач ОтносительныйПуть)
	
	Каталог = Новый Файл(ТекущийСценарий().Источник).Путь;
	Возврат Каталог + "\" + ОтносительныйПуть;
	
КонецФункции

Функция ВключенаОтладка()
	Возврат АргументыКоманднойСтроки.Количество() > 0 и АргументыКоманднойСтроки[0] = "-debug";
КонецФункции

Процедура СоздатьРаспаковщик()

	Если Распаковщик = Неопределено Тогда
		ПодключитьСценарий(АбсолютныйПуть("unpack.os"), "V83Unpack");
		Распаковщик = Новый V83Unpack;
		Если ВключенаОтладка() Тогда
			Распаковщик.РежимОтладки(Истина);
		КонецЕсли;
		
		ПараметрыИнициализации = Распаковщик.ПолучитьПараметрыИнициализации();
		Если Параметры.Свойство("ПутьКПлатформе83") Тогда
			ПараметрыИнициализации.ПутьКПлатформе83 = Параметры.ПутьКПлатформе83;
		КонецЕсли;
		ПараметрыИнициализации.ПутьGit = Параметры.ПутьGit;
		ПараметрыИнициализации.ДоменПочтыДляGit = Параметры.ДоменПочтыДляGit;
		Распаковщик.Инициализация(ПараметрыИнициализации);
		
	КонецЕсли;

КонецПроцедуры

Функция ПолучитьПараметрыИзОкружения()

	СИ = Новый СистемнаяИнформация();
	Окружение = СИ.ПеременныеСреды();
	
	Параметры = Новый Структура;
	Параметры.Вставить("ПутьКФайлуХранилища1С", Новый Файл(Окружение["storage_dir"]).ПолноеИмя + "\1cv8ddb.1CD");
	Параметры.Вставить("КаталогВыгрузки", Новый Файл(Окружение["git_local_repo"]).ПолноеИмя);
	Параметры.Вставить("ПутьКПлатформе83", Окружение["v8_executable"]);
	Параметры.Вставить("ПутьGit", Окружение["git_executable"]);
	Параметры.Вставить("GitURL", Окружение["git_remote_repo"]);
	Параметры.Вставить("ДоменПочтыДляGit", Окружение["git_email_domain"]);
	
	Возврат Параметры;

КонецФункции

ПрочитатьПараметры();
ПолучитьСУдаленногоУзла();
Синхронизировать();
ОтправитьНаУдаленныйУзел();
